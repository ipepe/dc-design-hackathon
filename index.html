<html>
<head>
    <title>DaftChristmas</title>
    <script type="text/javascript" src="lib/jquery.min.js"></script>
    <script type="text/javascript" src="lib/coffee-script.js"></script>
    <script type="text/javascript" src="https://threejs.org/build/three.js"></script>
    <script type="text/javascript" src="lib/STLLoader.js"></script>
    <script type="text/javascript" src="lib/ColladaLoader.js"></script>
    <style>body {background-color: black;margin: 0px;overflow: hidden;}</style>
</head>
<body>
    <div id="webgl-preview"></div>
    <script type="text/coffeescript">
    class window.ThreeJSFrame
        renderer: null
        preview: null
        constructor: ()->
            @renderer = new THREE.WebGLRenderer(antialias: true)
            @preview = $('#webgl-preview')
            @preview.append(@renderer.domElement)
            document.addEventListener('mousemove', (e)=> @?.onMouseMove(e))
            @camera = new THREE.PerspectiveCamera(45, 3, 0.1, 10000)
            @scene = new THREE.Scene()
            @scene.add(@camera)
            @cameraResizeForWindow()
            @loader = new THREE.ColladaLoader()
            @loader.load('scene.dae', ((collada)=>
                @model = collada.scene
                @scene.add(@model)
            ))
            window.addEventListener('resize', @cameraResizeForWindow, false)
            @?.onInitialize()
            @animate()

        cameraResizeForWindow: =>
            @camera.aspect = window.innerWidth / window.innerHeight;
            @camera.updateProjectionMatrix();
            @renderer.setSize(window.innerWidth, window.innerHeight)

        animate: ->
            @render()
            window.requestAnimationFrame(=> @animate())

        render: ->
            @?.onRender()
            @renderer.render(@scene, @camera)


    </script>
    <script type="text/coffeescript">
        class window.ThreeJSApp extends window.ThreeJSFrame
            onInitialize: ->
                @camera.position.set(-1, 1, 5)
                @camera.lookAt(new THREE.Vector3(0, 0.5, 0))
                @scene.fog = new THREE.Fog(0x000000, 4, 10)
                @createSnow()

            onMouseMove: (e)->
                x = e.clientX / window.innerWidth
                y = 1.5 - (e.clientY / window.innerHeight)
                @camera.lookAt(new THREE.Vector3(x, y,0))

            onRender: ->
                @model?.rotation.y += 0.003
                @simulateRainSnow()

            particles: new THREE.Geometry
            createSnow: (particleCount=100)->
                material = new THREE.PointCloudMaterial({
                    color: 0xFFFFFF,
                    size: 0.04,
                    blending: THREE.AdditiveBlending,
                    depthTest: false,
                    transparent: true
                })
                for i in [0..particleCount]
                    pX = Math.random()*10 - 5
                    pY = Math.random()*5 - 2.5
                    pZ = Math.random()*10 - 5
                    particle = new THREE.Vector3(pX, pY, pZ)
                    particle.velocity = {};
                    particle.velocity.y = -0.001;
                    @particles.vertices.push(particle)
                @scene.add(new THREE.PointCloud(@particles, material))

            simulateRainSnow: ->
                @particles.vertices.forEach (p)->
                    if p.y < -2
                        p.y = 2
                        p.velocity.y = -0.0012
                    p.velocity.y -= Math.random() * .00002
                    p.y += p.velocity.y
                @particles.verticesNeedUpdate = true

    </script>
    <script type="text/coffeescript">
        window.t = new window.ThreeJSApp()
    </script>
</body>
</html>