<html>
<head>
    <title>DaftChristmas</title>
    <script type="text/javascript" src="lib/jquery.min.js"></script>
    <script type="text/javascript" src="lib/coffee-script.js"></script>
    <script type="text/javascript" src="https://threejs.org/build/three.js"></script>
    <script type="text/javascript" src="lib/STLLoader.js"></script>
    <script type="text/javascript" src="lib/ColladaLoader.js"></script>
    <style>body {background-color: black;margin: 0px;overflow: hidden;}</style>
</head>
<body>
    <div id="webgl-preview"></div>
    <script type="text/coffeescript">
    class window.ThreeJSFrame
        renderer: null
        preview: null
        constructor: ()->
            @renderer = new THREE.WebGLRenderer(antialias: true)
            @preview = $('#webgl-preview')
            @preview.append(@renderer.domElement)
            document.addEventListener('mousemove', (e)=> @?.onMouseMove(e))
            @camera = new THREE.PerspectiveCamera(30, 3, 0.1, 10000)
            @scene = new THREE.Scene()
            @scene.add(@camera)
            @model_group = new THREE.Group()
            @scene.add(@model_group)
            @cameraResizeForWindow()
            @loader = new THREE.ColladaLoader()
            @loader.load('scene.dae', ((collada)=>
                @model = collada.scene
                @model_group.add(@model)
                @model.children[0].decay = 14
                @model.children[0].distance = 10
                @model.children[0].intensity = 10
            ))
            window.addEventListener('resize', @cameraResizeForWindow, false)
            @?.onInitialize()
            @animate()

        cameraResizeForWindow: =>
            @camera.aspect = window.innerWidth / window.innerHeight;
            @camera.updateProjectionMatrix();
            @renderer.setSize(window.innerWidth, window.innerHeight)

        animate: ->
            @render()
            window.requestAnimationFrame(=> @animate())

        render: ->
            @?.onRender()
            @renderer.render(@scene, @camera)


    </script>
    <script type="text/coffeescript">
        class window.ThreeJSApp extends window.ThreeJSFrame
            onInitialize: ->
                @camera.position.set(-1, 1, 5)
                @camera.lookAt(new THREE.Vector3(0, 1, 0))
                @scene.fog = new THREE.FogExp2(0x000000, 0.05)
                @createSnow()

            onMouseMove: (e)->
                x = e.clientX / window.innerWidth
                y = 1.5 - (e.clientY / window.innerHeight)
                @camera.lookAt(new THREE.Vector3(x, y,0))

            onRender: ->
                @model_group.rotation.y += 0.003
                @updateParticleSystem(@clock.getDelta()/2)

            rand: (v)-> (v * (Math.random() - 0.5))

            clock: new THREE.Clock()

            updateParticleSystem: (elapsed)->
                depth = @particleSystem.depth
                @particleSystem.geometry.verticesNeedUpdate = true
                @particleSystem.geometry.vertices.forEach (v)->
                    if v.y > 0
                        v.y -= elapsed
                    else
                        v.y = (depth/2 * Math.random()) + depth/2


            createSnow: (numParticles = 1000, depth = 10)->
                systemGeometry = new THREE.Geometry()
                systemMaterial = new THREE.PointsMaterial(color: 0xFFFFFF, size: 0.1, opacity: 0.5, transparent: true)
                for i in [0..numParticles]
                    systemGeometry.vertices.push(new THREE.Vector3(@rand(depth), @rand(depth), @rand(depth)))
                @model_group.add(@particleSystem = new THREE.Points(systemGeometry, systemMaterial))
                @particleSystem.depth = depth


    </script>
    <script type="text/coffeescript">
        window.t = new window.ThreeJSApp()
    </script>
</body>
</html>